// create a runnable jar with jar dependencies stored in lib subdirectory
tasks.whenTaskAdded { task ->
    ['startScripts', 'distTar'].each { String skipTaskName ->
        if (task.name.contains(skipTaskName)) {
            task.enabled = false
        }
    }
}
apply plugin: 'java'
apply plugin: 'application'

// JDK 8
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'edu.mcw.rgd.RNASeqPipeline.Manager'
String myAppName = 'RNASeqPipeline'

project.archivesBaseName = myAppName

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-dbcp:commons-dbcp:1.4'
    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-net:commons-net:3.5'
    implementation 'commons-pool:commons-pool:1.5.4'
    implementation 'org.apache.httpcomponents:httpclient:4.5.4'
    implementation 'log4j:log4j:1.2.17'
    implementation 'com.oracle.database.jdbc:ojdbc8:19.8.0.0'
    implementation 'org.springframework:spring-beans:5.2.3.RELEASE'
    implementation 'org.springframework:spring-jdbc:5.2.3.RELEASE'

    implementation fileTree(dir: 'lib', include: '*.jar')
}

jar {
    manifest {
        attributes(
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version'),
            'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

distributions {
    main {
        distributionBaseName = myAppName
    }
}

task createDistro(type: Copy) {
    def zipFile = file('build/distributions/'+myAppName+'.zip')
    def outputDir = file("build/install")

    from zipTree(zipFile)
    into outputDir
}
createDistro.dependsOn assembleDist
